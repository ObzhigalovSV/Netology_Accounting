
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	КурсВалюты = РегистрыСведений.КурсыВалют.КурсВалюты(Валюта, Дата);

	// регистр Управленческий 
	Движения.Управленческий.Записывать = Истина;
	Для Каждого Строка Из СписокОплат Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Касса;
		Движение.СчетКт = ПланыСчетов.Управленческий.Покупатели;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Сумма = Строка.Сумма * КурсВалюты;
		Движение.СуммаВалКт = Строка.Сумма;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Валюта] = Валюта;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент] = Строка.Контрагент;
		
		
	КонецЦикла;

	Движения.Управленческий.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходДенегСписокОплат.Контрагент КАК Контрагент,
		|	СУММА(ПриходДенегСписокОплат.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_Оплаты
		|ИЗ
		|	Документ.ПриходДенег.СписокОплат КАК ПриходДенегСписокОплат
		|ГДЕ
		|	ПриходДенегСписокОплат.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходДенегСписокОплат.Контрагент
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УправленческийОстатки.Субконто1 КАК Контрагент,
		|	УправленческийОстатки.СуммаВалОстаток КАК Задолженность
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(
		|			&Период,
		|			Счет = &СчетПокупатели,
		|			&СубконтоКонтрагентВалюта,
		|			Субконто1 В
		|					(ВЫБРАТЬ
		|						ВТ_Оплаты.Контрагент КАК Контрагент
		|					ИЗ
		|						ВТ_Оплаты КАК ВТ_Оплаты)
		|				И Организация = &Организация
		|				И Субконто2 = &Валюта) КАК УправленческийОстатки
		|ГДЕ
		|	УправленческийОстатки.СуммаВалОстаток < 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
	
	СубконтоКонтрагентВалюта = Новый Массив;
	СубконтоКонтрагентВалюта.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Контрагент);
	СубконтоКонтрагентВалюта.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Валюта);
	Запрос.УстановитьПараметр("СубконтоКонтрагентВалюта", СубконтоКонтрагентВалюта);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("По контрагенту %1 оплата превышает задолженность. Переплата %2 %3.", 
							Выборка.Контрагент,  - Выборка.Задолженность, Валюта);
			Сообщение.Сообщить();
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры
